apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    signingConfigs {
        release {
            Properties properties = getProperties('D:/Alarm-android/Alarm/keystore.properties')
            storeFile file(getProperty(properties, 'storeFile'))
            keyAlias getProperty(properties, 'keyAlias')
            storePassword getProperty(properties, 'storePassword')
            keyPassword getProperty(properties, 'keyPassword')
        }
    }

    compileSdkVersion 28

    defaultConfig {
        applicationId "com.mdkashif.universalarm"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "0.51." + getDate() + "_beta"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            versionNameSuffix "_r"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            versionNameSuffix "_d"
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }
}

def getProperties(String fileName) {
    final Properties properties = new Properties()
    def file = file(fileName)
    if (file.exists()) {
        file.withInputStream { stream -> properties.load(stream) }
    }
    return properties
}

static def getProperty(Properties properties, String name) {
    return properties.getProperty(name) ?: "$name missing"
}

static def getDate() {
    new Date().format('yyyyMMdd')
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    ////////////// Kotlin //////////////
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"

    ////////////// Crashlytics //////////////
    implementation 'com.google.firebase:firebase-crash:16.2.1'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true
    }

    ////////////// Miscellaneous //////////////
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'com.afollestad.material-dialogs:core:2.0.2'
    implementation 'com.github.jakob-grabner:Circle-Progress-View:v1.3'
    implementation 'com.pkmmte.view:circularimageview:1.1'
    implementation 'jp.wasabeef:blurry:2.1.1'
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.11.0'
    implementation 'com.appyvet:materialrangebar:1.4.4'
    implementation 'com.github.arimorty:floatingsearchview:2.1.1'
    implementation 'ca.antonious:materialdaypicker:0.4.0'
    implementation 'com.skyfishjy.ripplebackground:library:1.0.1'
    implementation 'com.wang.avi:library:2.1.3'
    implementation('com.github.omadahealth:lollipin:2.1.0@aar') {
        transitive = true
    }
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'
    implementation 'com.patloew.rxlocation:rxlocation:1.0.5'
    implementation 'com.hendraanggrian.recyclerview:recyclerview-expandable:0.1'
    implementation 'com.google.maps.android:android-maps-utils:0.5'
    implementation 'com.facebook.stetho:stetho:1.5.0' // for DB(Room)

    ////////////// Play Services //////////////
    implementation "com.google.android.gms:play-services-maps:$play_services_version"
    implementation "com.google.android.gms:play-services-location:$play_services_version"
    implementation "com.google.android.gms:play-services-places:$play_services_version"

    ////////////// Rx - Retrofit - Gson //////////////
    implementation 'io.reactivex.rxjava2:rxjava:2.2.5'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.google.code.gson:gson:2.8.5'

    ////////////// Support //////////////
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha09'
    implementation "androidx.preference:preference:1.1.0-rc01"

    ////////////// Room //////////////
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    ////////////// Koin //////////////
    implementation "org.koin:koin-android:$koin_version"

    ////////////// Testing //////////////
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    ////////////// Permissions //////////////
    implementation "org.permissionsdispatcher:permissionsdispatcher:$permission_dispatcher_version"
    kapt "org.permissionsdispatcher:permissionsdispatcher-processor:$permission_dispatcher_version"
}
apply plugin: 'com.google.gms.google-services'
